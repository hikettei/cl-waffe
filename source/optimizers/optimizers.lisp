
(in-package :cl-waffe.optimizers)

(defoptimizer SGD (params &key (lr 1e-3))
  :parameters ((params params) (lr lr))
  :update (()
	   (dotimes (i (hash-table-count (self params)))
	     ; W(n+1) = W(n) - n * grad
	     (mgl-mat:copy! (data (!sub (gethash i (self params))
					(!mul (self lr) (grad (gethash i (self params))))))
			    (data (gethash i (self params)))))))

(defoptimizer Momentum (params &key (momentum 0.9) (lr 1e-3))
  :parameters ((params params) (lr lr) (momentum momentum) (velocities (make-hash-table)))
  :update (()
	   (if (= (hash-table-count (self velocities)) 0)
	       (progn
		 (dotimes (i (hash-table-count (self params)))
		   (setf (gethash i (self velocities)) 0))))
	   (dotimes (i (hash-table-count (self params)))
	     ; v(n+1) = momentum*v(n) - grad*lr
	     ; w(n+1) = w(n) + v(n+1)
	     (setf (gethash i (self velocities)) (data (!sub (!mul (self momentum) (gethash i (self velocities)))
							     (!mul (self lr) (grad (gethash i (self params)))))))
	     (mgl-mat:copy! (data (!add (gethash i (self velocities))
					(gethash i (self params))))
			    (data (gethash i (self params)))))))

(defoptimizer AdaGrad (params &key (lr 1e-3) (epsilon 1e-7))
  :parameters ((params params) (lr lr) (h (make-hash-table)) (epsilon epsilon))
  :update (()
	   (if (= (hash-table-count (self h)) 0)
	       (dotimes (i (hash-table-count (self params)))
		 (setf (gethash i (self h)) 0)))
	   (dotimes (i (hash-table-count (self params)))
	     ; h(t+1) = h(t) + (grad * grad)
             ; w(t+1) = w(t) - {lr * grad}/sqrt(h(t+1))
	     (setf (gethash i (self h)) (data (!add (gethash i (self h))
						(!mul (grad (gethash i (self params)))
						      (grad (gethash i (self params)))))))
	     (mgl-mat:copy! (data (!sub (data (gethash i (self params)))
					(!div (!mul (self lr) (grad (gethash i (self params))))
					      (!add (!sqrt (gethash i (self h))) (self epsilon)))))
			    (data (gethash i (self params)))))))

(defoptimizer RMSProp (params &key (lr 1e-3) (epsilon 1e-7) (decay-rate 0.99))
  :parameters ((params params) (lr lr) (h (make-hash-table)) (epsilon epsilon) (decay-rate 0.99))
  :update (()
	   (if (= (hash-table-count (self h)) 0)
	       (dotimes (i (hash-table-count (self params)))
		 (setf (gethash i (self h)) 0)))
	   (dotimes (i (hash-table-count (self params)))
             (setf (gethash i (self h)) (data (!mul (gethash i (self h)) (self decay-rate))))
	     (setf (gethash i (self h)) (data (!add (gethash i (self h))
						(!mul (!mul (!sub 1.0 (self decay-rate))
							    (grad (gethash i (self params))))
						      (grad (gethash i (self params)))))))
	     (mgl-mat:copy! (data (!sub (data (gethash i (self params)))
					(!div (!mul (self lr) (grad (gethash i (self params))))
					      (!add (!sqrt (gethash i (self h))) (self epsilon)))))
			    (data (gethash i (self params)))))))

(defoptimizer Adam (params &key (lr 1e-3) (epsilon 1e-7) (beta1 0.9) (beta2 0.999))
  :parameters ((params params) (lr lr) (m (make-hash-table)) (v (make-hash-table)) (i 0) (epsilon epsilon) (beta1 beta1) (beta2 beta2))
  :update (()
	   (if (= (hash-table-count (self m)) 0)
	       (dotimes (i (hash-table-count (self params)))
		 (setf (gethash i (self m)) 0)
		 (setf (gethash i (self v)) 0)))
	   (incf (self i) 1)
	   (let ((lr-t (!mul (self lr) (!div (!sqrt (!sub 1.0 (!pow (self beta2) (self i))))
					     (!sub 1.0 (!pow (self beta1) (self i)))))))
	     (dotimes (i (hash-table-count (self params)))
	       (setf (gethash i (self m)) (data (!add (gethash i (self m))
						  (!mul (!sub 1 (self beta1))
							(!sub (grad (gethash i (self params)))
							      (gethash i (self m)))))))
	       
	       (setf (gethash i (self v)) (data (!add (gethash i (self v))
						  (!mul (!sub 1 (self beta2))
							(!sub (!pow (grad (gethash i (self params))) 2)
							      (gethash i (self v)))))))
	       
	       (mgl-mat:copy! (data (!sub (gethash i (self params))
					  (!mul lr-t (!div
						      (gethash i (self m))
						      (!add (!sqrt (gethash i (self v))) (self epsilon))))))
			      (data (gethash i (self params))))))))

